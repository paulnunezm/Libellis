apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'

def versionMajor = 0
def versionMinor = 4
def versionPatch = 0

// Every time a new branch is added to the dev branch or a build uploaded to the beta or alpha we sum +1 to this
def versionBuild = 0

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "com.nunez.libellis.ni"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }
    buildTypes.each {
        it.buildConfigField 'String', 'GOODREADS_API_KEY', project.API_KEY
        it.buildConfigField 'String', 'GOODREADS_API_SECRET', project.API_SECRET
        it.buildConfigField 'String', 'CALLBACK_URL', project.CALLBACK_URL
    }
    sourceSets {
        test {
            resources.srcDirs += ['src/test/resources']
        }
        androidTest {
            resources.srcDirs += ['src/androidTest/resources']
        }
    }
    testOptions {
        unitTests.all {
            // All the usual Gradle options.
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }
}

kapt {
    generateStubs = true
}


ext.supportVersion = "26.1.0"
ext.daggerVersion = "2.11-rc2"

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':oauthme')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Android support
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha01'
    implementation 'com.google.android.material:material:1.1.0-alpha01'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'

    // Square
    implementation 'com.squareup.okhttp3:okhttp:3.8.1'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation('com.squareup.retrofit2:converter-simplexml:2.1.0') {
        exclude group: 'xpp3', module: 'xpp3'
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
    }

    // Signed requests
    implementation 'se.akerfeldt:okhttp-signpost:1.1.0'

    // Play services
    implementation 'com.google.android.gms:play-services-plus:11.4.2'

    // Realm extensions
    implementation "com.github.vicpinm:krealmextensions:2.0.0-beta2"

    // RxJava & RxAndroid
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    implementation 'io.reactivex.rxjava2:rxjava:2.1.5'

    // Shimmer
    implementation 'com.facebook.shimmer:shimmer:0.1.0@aar'

    // Tests
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1-beta01'
    androidTestImplementation 'androidx.test:runner:1.1.1-beta01'
    androidTestImplementation 'androidx.annotation:annotation:1.0.1'
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation 'junit:junit:4.12'
    testImplementation 'com.nhaarman:mockito-kotlin:1.5.0'

    // Debugging
    debugImplementation 'com.facebook.stetho:stetho:1.5.0'
    debugImplementation 'com.facebook.stetho:stetho-okhttp3:1.4.2'
}
repositories {
    mavenCentral()
    google()
    jcenter()
}
